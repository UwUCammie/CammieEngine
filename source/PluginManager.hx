package;

import flixel.system.frontEnds.CameraFrontEnd;
import flixel.system.frontEnds.BitmapFrontEnd;
import flixel.system.FlxAssets.FlxSoundAsset;
import flixel.sound.FlxSound;
import flixel.group.FlxGroup.FlxTypedGroup;
import flixel.group.FlxSpriteGroup.FlxTypedSpriteGroup;
import flixel.effects.FlxFlicker;
import flixel.sound.FlxSoundGroup;
import flixel.system.frontEnds.SoundFrontEnd;
import openfl.display.DisplayObject;
import openfl.display.Bitmap;
import flixel.input.keyboard.FlxKeyboard;
import flixel.system.frontEnds.InputFrontEnd;
import flixel.math.FlxRect;
import flixel.FlxState;
import openfl.display.Stage;
import flixel.math.FlxPoint;
import flixel.FlxGame;
import flixel.input.gamepad.FlxGamepadManager;
import flixel.FlxCamera;
import flixel.util.FlxColor;
import flixel.util.FlxStringUtil;
import flixel.text.FlxText;
import flixel.text.FlxText.FlxTextBorderStyle;
import ExtraStrumlineAdapter.ExtraStrumlineNoteStyleRegistry;
import HxcNoteStyleCompat.HxcNoteSplashCompat;
import ExtraStrumlineAdapter.ExtraStrumlineRhythm;
import HxcHealthIconAdapter;
import flixel.ui.FlxButton;
import flixel.tweens.FlxEase;
import flixel.addons.effects.FlxTrail;
import flixel.addons.effects.chainable.FlxEffectSprite;
import flixel.addons.effects.chainable.FlxGlitchEffect;
import plugins.tools.MetroSprite;
import hscript.InterpEx;
import hscript.Interp;
import flixel.FlxG;
import lime.system.System;
import sys.io.File;
import sys.FileSystem;
import haxe.ds.StringMap;
import lime.utils.Assets;

import animate.FlxAnimate;
import animate.FlxAnimateFrames;

class PluginManager {
    public static var interp = new InterpEx();
    public static var hscriptClasses:Array<String> = [];
	@:access(hscript.InterpEx)
    private var flxPointWorkaround = {};
    public static function init() {
        var filelist = hscriptClasses = CoolUtil.coolTextFile("assets/scripts/plugin_classes/classes.txt");
		interp = addVarsToInterp(interp);
        HscriptGlobals.init();
        for (file in filelist) {
            if (FNFAssets.exists("assets/scripts/plugin_classes/" + file + ".hx")) {
				interp.addModule(FNFAssets.getText("assets/scripts/plugin_classes/" + file + '.hx'));
            }
        }
        //trace(InterpEx._scriptClassDescriptors);

        //for (thingy in Reflect.fields(FlxPoint))
            //Reflect.setProperty(flxPointWorkaround, thingy);
    }
    /**
     * Create a simple interp, that already added all the needed shit
     * This is what has all the default things for hscript.
     * @see https://github.com/TheDrawingCoder-Gamer/Funkin/wiki/HScript-Commands
     * @return Interp
     */
    public static function createSimpleInterp():Interp {
        var reterp = new Interp();
        reterp = addVarsToInterp(reterp);
        return reterp;
    }
    
    public static function addVarsToInterp<T:Interp>(interp:T):T {
		interp.variables.set("Conductor", Conductor);
		interp.variables.set("FlxSprite", DynamicSprite);
        interp.variables.set("DisSprite", DisSprite);
        interp.variables.set("FlxTiledSprite", DynamicSprite.DynamicTiledSprite);
        interp.variables.set("FlxBackdrop", DynamicSprite.DynamicBackdrop);
        interp.variables.set("getFlxPoint", function(x, y) {return FlxPoint.get(x, y);});
		interp.variables.set("FlxSound", DynamicSound);
		interp.variables.set("FlxAtlasFrames", DynamicSprite.DynamicAtlasFrames);
        interp.variables.set("FlxAnimate", FlxAnimate);
        interp.variables.set("FlxAnimateFrames", FlxAnimateFrames);
		interp.variables.set("FlxGroup", flixel.group.FlxGroup);
		interp.variables.set("FlxTypedGroup", FlxTypedGroup);
		interp.variables.set("FlxTypedSpriteGroup", FlxTypedSpriteGroup);
		interp.variables.set("FlxFlicker", FlxFlicker);
		interp.variables.set("FlxAngle", flixel.math.FlxAngle);
		interp.variables.set("FlxMath", flixel.math.FlxMath);
		// HXC modules use these concrete Flixel utility classes directly.  They
		// are native engine APIs, so expose the same live classes used by source
		// code instead of leaving the rooted module gate to guess at donor names.
		interp.variables.set("FlxColor", HxcFlxColorCompat);
		interp.variables.set("FlxStringUtil", FlxStringUtil);
		interp.variables.set("TitleState", TitleState);
		interp.variables.set("MainMenuState", MainMenuState);
		// HXC character callbacks refer to the donor spelling
		// `GameOverSubState`; this alias is only a class boundary. Static suffix
		// writes are lowered by HxcCompat to HxcCompatRuntime, while native
		// substate behavior remains owned by these local classes.
		interp.variables.set("GameOverSubState", GameOverSubstate);
		interp.variables.set("PauseSubState", PauseSubState);
        interp.variables.set("CoolUtil", CoolUtil);
        interp.variables.set("coolTextFile", CoolUtil.coolTextFile);
		interp.variables.set("makeRangeArray", CoolUtil.numberArray);
		interp.variables.set("FNFAssets", FNFAssets);
		// ported char scripts reference Paths.* (getCharacterJson for JSON
		// atlases, getSparrowAtlas/image in stages/modcharts)
        interp.variables.set("Paths", Paths);
		interp.variables.set("Assets", Assets);
		interp.variables.set("Bitmap", Bitmap);
		interp.variables.set("StringMap", StringMap);
		// : )
        interp.variables.set("System", lime.system.System);
        interp.variables.set("File", sys.io.File);
        interp.variables.set("FileSystem", sys.FileSystem);
		interp.variables.set("FlxG", HscriptGlobals);
		interp.variables.set("FlxTimer", flixel.util.FlxTimer);
        interp.variables.set("FlxObject", flixel.FlxObject);
		interp.variables.set("FlxTween", flixel.tweens.FlxTween);
        interp.variables.set("FlxCamera", flixel.FlxCamera);
		interp.variables.set("FlxText", flixel.text.FlxText);
		interp.variables.set("FlxTextBorderStyle", FlxTextBorderStyle);
		interp.variables.set("FlxButton", FlxButton);
        interp.variables.set("SHADOW", FlxTextBorderStyle.SHADOW);
        interp.variables.set("OUTLINE", FlxTextBorderStyle.OUTLINE);
        interp.variables.set("OUTLINE_FAST", FlxTextBorderStyle.OUTLINE_FAST);
        interp.variables.set("FlxBar", flixel.ui.FlxBar);
		interp.variables.set("Std", Std);
		interp.variables.set("StringTools", StringTools);
		interp.variables.set("MetroSprite", MetroSprite);
		interp.variables.set("FlxRuntimeShader", ShaderHandler.CoolRuntimeShader);
		interp.variables.set("ShaderFilter", openfl.filters.ShaderFilter);
		interp.variables.set("DropShadowShader", shaders.DropShadowShader);
		interp.variables.set("FlxTrail", FlxTrail);
		// Springless and other imported modcharts construct these chainable
		// effects directly. Keeping them in the common interpreter prevents a
		// missing class from aborting start() halfway through a camera fade.
		interp.variables.set("FlxEffectSprite", FlxEffectSprite);
		interp.variables.set("FlxGlitchEffect", FlxGlitchEffect);
		interp.variables.set("FlxEase", FlxEase);
		interp.variables.set("Reflect", Reflect);
		interp.variables.set("Character", Character);
		// Imported V-Slice song scripts use a second Strumline only for their
		// own copied note stream.  These aliases point at the bounded native
		// adapter; they do not expose donor Strumline/NoteStyle instances.
		interp.variables.set("Strumline", ExtraStrumlineAdapter);
		interp.variables.set("HxcHealthIconAdapter", HxcHealthIconAdapter);
		interp.variables.set("NoteStyleRegistry", ExtraStrumlineNoteStyleRegistry);
		interp.variables.set("HxcNoteStyleCompat", HxcNoteStyleCompat);
		interp.variables.set("HxcNoteSplashCompat", HxcNoteSplashCompat);
		interp.variables.set("GRhythmUtil", ExtraStrumlineRhythm);
		// HXC modules use the native control singleton for menu navigation. Keep
		// the live PlayerSettings object available in every interpreter instead
		// of treating its authored reads as donor-only globals.
		interp.variables.set("PlayerSettings", PlayerSettings);
			interp.variables.set("OptionsHandler", OptionsHandler);
			// Generated HXC adapters use this namespaced store for the small common
			// Save/Preferences surface; never expose the donor singleton itself.
			interp.variables.set("HxcCompatRuntime", HxcCompatRuntime);
			interp.variables.set("hxcSetWindowTitle", HxcWindowCompat.setTitle);
			interp.variables.set("hxcSetWindowIcon", HxcWindowCompat.setIcon);
			interp.variables.set("hxcStateInit", HxcCompatRuntime.stateInit);
			// Generic interpreters have no manifest owner. Keep the same helpers
			// available for generated HXC adapters, but an empty root permits only
			// the explicit native aliases and rejects foreign state/substate names.
			interp.variables.set("hxcSubStateInit", function(name:Dynamic):Dynamic
				return HxcStateFactory.subStateInit('', name));
			interp.variables.set("hxcStateFactory", function(name:Dynamic, ?args:Array<Dynamic>):Dynamic
				return HxcStateFactory.stateFactory('', name, args));
			interp.variables.set("hxcSwitchState", function(target:Dynamic):Bool
				return HxcStateFactory.switchStateScoped('', target));
			interp.variables.set("hxcStartExitState", function(target:Dynamic):Bool
				return HxcStateFactory.startExitState('', target));
			interp.variables.set("hxcOpenSubState", function(target:Dynamic):Bool
				return HxcStateFactory.openSubStateScoped('', null, target));
			interp.variables.set("hxcOpenSubStateOn", function(host:Dynamic, target:Dynamic):Bool
				return HxcStateFactory.openSubStateScoped('', host, target));
			interp.variables.set("DifficultyManager", DifficultyManager);
			interp.variables.set("CreditsState", CreditsState);
			interp.variables.set("SaveDataState", SaveDataState);
		#if debug
		interp.variables.set("debug", true);
		#else
		interp.variables.set("debug", false);
		#end
        return interp;
    }
}
class HscriptGlobals {
    public static var VERSION = FlxG.VERSION;
    public static var autoPause(get, set):Bool;
    public static var bitmap(get, never):BitmapFrontEnd;
    // no bitmapLog
    public static var camera(get ,set):FlxCamera;
    public static var cameras(get, never):CameraFrontEnd;
    // no console frontend
    // no debugger frontend
    public static var drawFramerate(get, set):Int;
    public static var elapsed(get, never):Float;
    public static var fixedTimestep(get, set):Bool;
    public static var fullscreen(get, set):Bool;
    public static var game(get, never):FlxGame;
    public static var gamepads(get, never):FlxGamepadManager;
    public static var height(get, never):Int;
	public static var initialHeight(get, never):Int;
	public static var initialWidth(get, never):Int;
	public static var onMobile(get, never):Bool;
    //public static var initialZoom(get, never):Float;
    public static var inputs(get, never):InputFrontEnd;
    public static var keys(get, never):FlxKeyboard;
    // no log
    public static var maxElapsed(get, set):Float;
    public static var mouse = FlxG.mouse;
    // no plugins
    public static var random = FlxG.random;
    public static var renderBlit(get, never):Bool;
    public static var renderMethod(get, never):FlxRenderMethod;
    public static var renderTile(get, never):Bool;
    // no save because there are other ways to access it and i don't trust you guys
    public static var sound(default, null):HscriptSoundFrontEndWrapper;
    public static var stage(get, never):Stage;
    public static var state(get, never):FlxState;
    // no swipes because no mobile : )
    public static var timeScale(get, set):Float;
    // flixel only compiles FlxTouchManager under FLX_TOUCH (mobile targets),
    // but ported V-Slice menus iterate FlxG.touches.list unconditionally, so
    // the surrogate must exist on desktop too: an empty touch list is exactly
    // what the real manager holds without a touch screen. Leaving the member
    // null made for-in reach hscript makeIterator(null) and SIGSEGV.
    public static var touches(get, never):HscriptTouchFrontEnd;
    public static var updateFramerate(get,set):Int;
    // no vcr : )
    // no watch : )
    public static var width(get, never):Int;
    public static var worldBounds(get, never):FlxRect;
    public static var worldDivisions(get, set):Int;
    public static function init() {
        sound = new HscriptSoundFrontEndWrapper(FlxG.sound);
    }
    static function get_bitmap() {
        return FlxG.bitmap;
    }
    static function get_cameras() {
        return FlxG.cameras;
    }
    static function get_autoPause():Bool {
        return FlxG.autoPause;
    }
    static function set_autoPause(b:Bool):Bool {
        return FlxG.autoPause = b;
    }
	static function get_drawFramerate():Int
	{
		return FlxG.drawFramerate;
	}

	static function set_drawFramerate(b:Int):Int
	{
		return FlxG.drawFramerate = b;
	}
    static function get_elapsed():Float {
        return FlxG.elapsed;
    }
	static function get_fixedTimestep():Bool
	{
		return FlxG.fixedTimestep;
	}

	static function set_fixedTimestep(b:Bool):Bool
	{
		return FlxG.fixedTimestep = b;
	}
	static function get_fullscreen():Bool
	{
		return FlxG.fullscreen;
	}

	static function set_fullscreen(b:Bool):Bool
	{
		return FlxG.fullscreen = b;
	}
    static function get_height():Int {
        return FlxG.height;
    }
    static function get_initialHeight():Int {
        return FlxG.initialHeight;
    }
    static function get_camera():FlxCamera {
        return FlxG.camera;
    }
    static function set_camera(c:FlxCamera):FlxCamera {
        return FlxG.camera = c;
    }
    static function get_game():FlxGame {
        return FlxG.game;
    }
    static function get_gamepads():FlxGamepadManager {
        return FlxG.gamepads;
    }
	static function get_initialWidth():Int {
		return FlxG.initialWidth;
	}
	static function get_onMobile():Bool {
		return FlxG.onMobile;
	}
    /*static function get_initialZoom():Float {
        return FlxG.initialZoom;
    }*/
    static function get_inputs() {
        return FlxG.inputs;
    }
    static function get_keys() {
        return FlxG.keys;
    }
    static function set_maxElapsed(s) {
        return FlxG.maxElapsed = s;
    }
    static function get_maxElapsed() {
        return FlxG.maxElapsed;
    }
    static function get_renderBlit() {
        return FlxG.renderBlit;
    }
    static function get_renderMethod() {
        return FlxG.renderMethod;
    }
    static function get_renderTile() {
        return FlxG.renderTile;
    }
    static function get_stage() {
        return FlxG.stage;
    }
    static function get_state() {
        return FlxG.state;
    }
    static function get_touches():HscriptTouchFrontEnd {
        return HscriptTouchFrontEnd.instance;
    }
    static function set_timeScale(s) {
        return FlxG.timeScale = s;
    }
    static function get_timeScale() {
        return FlxG.timeScale;
    }
    static function set_updateFramerate(s) {
        return FlxG.updateFramerate = s;
    }
    static function get_updateFramerate() {
        return FlxG.updateFramerate;
    }
    static function get_width() {
        return FlxG.width;
    }
    static function get_worldBounds() {
        return FlxG.worldBounds;
    }
    static function get_worldDivisions() {
        return FlxG.worldDivisions;
    }
	static function set_worldDivisions(s) {
		return FlxG.worldDivisions = s;
	}

    public static function addChildBelowMouse<T:DisplayObject>(Child:T, IndexModifier:Int = 0):T {
        return FlxG.addChildBelowMouse(Child, IndexModifier);
    }
    public static function addPostProcess(postProcess) {
        return null; //FlxG.addPostProcess(postProcess);
    }
    public static function collide(?ObjectOrGroup1, ?ObjectOrGroup2, ?NotifyCallback) {
        return FlxG.collide(ObjectOrGroup1, ObjectOrGroup2, NotifyCallback);
    }
    // no open url because i don't trust you guys

	public static function overlap(?ObjectOrGroup1, ?ObjectOrGroup2, ?NotifyCallback, ?ProcessCallback)
	{
		return FlxG.overlap(ObjectOrGroup1, ObjectOrGroup2, NotifyCallback, ProcessCallback);
	}
    public static function pixelPerfectOverlap(Sprite1, Sprite2, AlphaTolerance = 255, ?Camera) {
        return FlxG.pixelPerfectOverlap(Sprite1, Sprite2, AlphaTolerance, Camera);
    }
    public static function removeChild<T:DisplayObject>(Child:T):T {
        return FlxG.removeChild(Child);
    }
    public static function removePostProcess(postProcess) {
        return null; //FlxG.removePostProcess(postProcess);
    }
    // no reset game or reset state because i don't trust you guys
    public static function resizeGame(Width, Height) {
        FlxG.resizeGame(Width, Height);
    }
    public static function resizeWindow(Width, Height) {
        FlxG.resizeWindow(Width, Height);
    }
    // no switch state because i don't trust you guys
}

/**
    Minimal desktop stand-in for flixel's FlxTouchManager (compiled only under
    FLX_TOUCH).  Ported donor menus read FlxG.touches.list / getFirst() without
    checking FlxG.onMobile first; an empty list and a null first touch are
    exactly what the real manager holds on a desktop, so those scripts see
    donor-faithful data instead of a null manager.
*/
class HscriptTouchFrontEnd {
    public static var instance:HscriptTouchFrontEnd = new HscriptTouchFrontEnd();

    public var list:Array<Dynamic> = [];

    function new() {}

    public function getFirst():Dynamic {
        return list.length == 0 ? null : list[0];
    }
}

class HscriptSoundFrontEndWrapper {

    var wrapping:SoundFrontEnd;
    public var defaultMusicGroup(get, set):FlxSoundGroup;
    public var defaultSoundGroup(get, set):FlxSoundGroup;
    public var list(get, never):FlxTypedGroup<FlxSound>;
    public var music (get, set):FlxSound;
    // no mute keys because why do you need that
    // no muted because i don't trust you guys
    // no soundtray enabled because i'm lazy 
    // no volume because i don't trust you guys
    function get_defaultMusicGroup() {
        return wrapping.defaultMusicGroup;
    }
    function set_defaultMusicGroup(a) {
        return wrapping.defaultMusicGroup = a;
    }
    function get_defaultSoundGroup() {
        return wrapping.defaultSoundGroup;
    }
    function set_defaultSoundGroup(a) {
        return wrapping.defaultSoundGroup = a;
    }
    function get_list() {
        return wrapping.list;
    }
    function get_music() {
        return wrapping.music;
    }
    function set_music(a) {
        return wrapping.music = a;
    }
    public function load(?EmbeddedSound:FlxSoundAsset, Volume = 1.0, Looped = false, ?Group, AutoDestroy = false, AutoPlay = false, ?URL, ?OnComplete) {
        if ((EmbeddedSound is String)) {
            var sound = FNFAssets.getSound(EmbeddedSound);
            return wrapping.load(sound, Volume, Looped, Group, AutoDestroy, AutoPlay, URL, OnComplete);
        }
        return wrapping.load(EmbeddedSound, Volume, Looped, Group, AutoDestroy, AutoPlay, URL, OnComplete);
    }
    public function pause() {
        wrapping.pause();
    }
    public function play(EmbeddedSound:FlxSoundAsset, Volume = 1.0, Looped = false, ?Group, AutoDestroy = true, ?OnComplete) {
        if ((EmbeddedSound is String)) {
            var sound = FNFAssets.getSound(EmbeddedSound);
            return wrapping.play(sound, Volume, Looped, Group, AutoDestroy, OnComplete);
        }
        return wrapping.play(EmbeddedSound, Volume, Looped, Group, AutoDestroy, OnComplete);
    }

    public function playMusic(Music:FlxSoundAsset,Volume= 1.0, Looped = true, ?Group ) {
        if ((Music is String)) {
            var sound = FNFAssets.getSound(Music);
            wrapping.playMusic(sound, Volume, Looped, Group);
            return;
        }
        wrapping.playMusic(Music, Volume, Looped, Group);        

    }
    public function resume() {
        wrapping.resume();
    }
    public function new(wrap:SoundFrontEnd) {
        wrapping = wrap;
    }
}
